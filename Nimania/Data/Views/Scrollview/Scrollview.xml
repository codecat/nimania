<frame id="scroll_frame1_<?=scrollid?>" clip="1" clipsizen="<?=width?> <?=height?>">
	<frame id="scroll_frame2_<?=scrollid?>">
		<frame id="scroll_contents_<?=scrollid?>">
			<?=contents?>
		</frame>
	</frame>
</frame>

<quad id="scroll_trough_<?=scrollid?>" posn="<?=width?> 0" sizen="4 <?=height?>" bgcolor="2227" />
<quad id="scroll_handle_<?=scrollid?>" posn="<?=width?> 0 1" sizen="4 20" style="Bgs1InRace" substyle="BgCard" scriptevents="1" />

<script><!--
	declare Boolean scrollInit_<?=scrollid?>;
	declare Boolean overHandle_<?=scrollid?>;
	declare Real overHandleY_<?=scrollid?>;

	Void Scrollview_<?=scrollid?>()
	{
		declare scrollContents <=> (Page.GetFirstChild("scroll_contents_<?=scrollid?>") as CMlFrame);
		declare scrollTrough <=> (Page.GetFirstChild("scroll_trough_<?=scrollid?>") as CMlQuad);
		declare scrollHandle <=> (Page.GetFirstChild("scroll_handle_<?=scrollid?>") as CMlQuad);
		declare scrollHeight = <?=contentsheight?>;

		if (!scrollInit_<?=scrollid?>) {
			declare scrollFrame1 <=> (Page.GetFirstChild("scroll_frame1_<?=scrollid?>") as CMlFrame);
			declare scrollFrame2 <=> (Page.GetFirstChild("scroll_frame2_<?=scrollid?>") as CMlFrame);

			declare width = scrollFrame1.ClipWindowSize.X;
			declare height = scrollFrame1.ClipWindowSize.Y;
			scrollFrame1.RelativePosition.X = (width / 2.0 - 1.5);
			scrollFrame1.RelativePosition.Y = -(height / 2.0);
			scrollFrame2.RelativePosition.X = -(width / 2.0 - 1.5);
			scrollFrame2.RelativePosition.Y = (height / 2.0);

			scrollInit_<?=scrollid?> = True;
		}

		if (scrollContents == Null || scrollTrough == Null || scrollHandle == Null) {
			return;
		}

		if (scrollHeight < scrollTrough.Size.Y) {
			scrollHandle.Size.Y = scrollTrough.Size.Y;
			return;
		}

		declare handleSizeFactor = scrollTrough.Size.Y / scrollHeight;
		if (handleSizeFactor < 0.05) {
			handleSizeFactor = 0.05;
		}
		scrollHandle.Size.Y = handleSizeFactor * scrollTrough.Size.Y;

		foreach (Event in PendingEvents) {
			log(Event.Type);
			if (Event.Control == scrollHandle) {
				if (Event.Type == CMlEvent::Type::MouseClick) {
					overHandle_<?=scrollid?> = True;
					overHandleY_<?=scrollid?> = MouseY - scrollHandle.AbsolutePosition.Y;
				}
			}
		}

		if (MouseLeftButton && overHandle_<?=scrollid?>) {
			declare newY = MouseY - scrollTrough.AbsolutePosition.Y - overHandleY_<?=scrollid?>;
			if (newY > 0.0) {
				newY = 0.0;
			} else if (newY < -(scrollTrough.Size.Y - scrollHandle.Size.Y)) {
				newY = -(scrollTrough.Size.Y - scrollHandle.Size.Y);
			}
			scrollHandle.RelativePosition.Y = newY;

			declare factor = -newY / (scrollTrough.Size.Y - scrollHandle.Size.Y);
			scrollContents.RelativePosition.Y = (factor * (scrollHeight - scrollTrough.Size.Y));
		}

		if (!MouseLeftButton) {
			overHandle_<?=scrollid?> = False;
		}
	}
--></script>
